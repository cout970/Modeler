group 'com.cout970'
version '2.0.0-pre-beta'

buildscript {
    ext.kotlin_version = '1.2.31'
    ext.lwjglVersion = "3.1.6-SNAPSHOT"
    ext.legui_version = "1.4.5-226"
    ext.arrow_version = "0.7.2"

    repositories {
        mavenCentral()
        maven {
            name = "kotlin-eap"
            url = "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "com.cout970.modeler.MainKt"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "Releases"
        url = "https://raw.github.com/LiquidEngine/repo/releases"
    }
    maven {
        name = "Snapshots"
        url = "https://raw.github.com/LiquidEngine/repo/snapshots"
    }
    maven {
        name = "Develop"
        url = "https://raw.github.com/LiquidEngine/repo/develop"
    }
    maven {
        name = "kotlin-eap"
        url = "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"
    }
    maven {
        name = "kotlinx"
        url = "http://dl.bintray.com/kotlin/kotlinx"
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile files(
            "./lib/GlUtilities-1.0.5.jar",
            "./lib/MatrixLib-1.0.3.jar",
            "./lib/RaytraceLib-1.0.3.jar",
            "./lib/VectorLib-1.0.3.jar",
            "./lib/CollisionLib-1.0.4.jar",
            "./lib/reactive-1.3.0.jar")

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.1"
    compile "org.liquidengine:legui:$legui_version"

    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.22.1'

    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-egl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-nanovg:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-sse:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-nfd:$lwjglVersion"
    compile "org.lwjgl:lwjgl-xxhash:${lwjglVersion}"

    compile "io.arrow-kt:arrow-core:$arrow_version"
    compile "io.arrow-kt:arrow-syntax:$arrow_version"
    compile "io.arrow-kt:arrow-typeclasses:$arrow_version"
    compile "io.arrow-kt:arrow-data:$arrow_version"
    compile "io.arrow-kt:arrow-instances-core:$arrow_version"
    compile "io.arrow-kt:arrow-instances-data:$arrow_version"

    testCompile group: 'junit', name: 'junit', version: '4.11'

    // Download all native bindings, so after compilation the result works in all platforms
    ["natives-windows", "natives-linux", "natives-macos"].each { lwjglNatives ->
        compile "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-nanovg:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-sse:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-nfd:$lwjglVersion:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-xxhash:${lwjglVersion}:${lwjglNatives}"
    }
}

jar {
    manifest {
        attributes "Main-Class": "com.cout970.modeler.MainKt"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from sourceSets.main.allSource
}

compileKotlin {
    incremental = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}