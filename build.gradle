group 'com.cout970'
version '1.3.2-SNAPSHOT'

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}


buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
        maven {
            name = "kotlin-eap"
            url = "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "com.cout970.modeler.MainKt"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "Releases"
        url = "https://raw.github.com/LiquidEngine/repo/releases"
    }
    maven {
        name = "Snapshots"
        url = "https://raw.github.com/LiquidEngine/repo/snapshots"
    }
    maven {
        name = "kotlin-eap"
        url = "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

project.ext.lwjglVersion = "3.1.2-SNAPSHOT"
project.ext.legui_version = "1.1.8-SNAPSHOT"

dependencies {
    compile files(
            "./lib/GlUtilities-1.0.jar",
            "./lib/MatrixLib-1.0.jar",
            "./lib/RaytraceLib-1.0.jar",
            "./lib/VectorLib-1.0.jar")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.liquidengine:legui:$legui_version"
    compile "org.funktionale:funktionale-all:1.1"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-egl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-nanovg:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-sse:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-xxhash:${lwjglVersion}"

    testCompile group: 'junit', name: 'junit', version: '4.11'

    // Native bindings
    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-nanovg:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-sse:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-xxhash:${lwjglVersion}:${lwjglNatives}"
}

jar {
    manifest {
        attributes "Main-Class": "com.cout970.modeler.MainKt"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from sourceSets.main.allSource
}
kotlin {
    experimental {
        coroutines "enable"
    }
}